#!/usr/bin/env racket
#lang reader (submod "support/lang/script.rkt" reader)

(require (for-syntax racket/base)
         json
         net/base64
         net/head
         net/qp
         net/smtp
         openssl
         racket/contract
         racket/format
         racket/list
         racket/match
         racket/port
         racket/random
         racket/string
         racket/system
         syntax/parse/define
         xml
         "support/error.rkt"
         "support/resource.rkt"
         "support/tree-parse.rkt")

(define-logger send-pandoc-email)

;; ---------------------------------------------------------------------------------------------------

(define (open-input-empty) (open-input-bytes #""))

(define mime-type:plain-text "text/plain;charset=utf-8")
(define mime-type:html "text/html;charset=utf-8")
(define (mime-type:alternative boundary)
  (data-lines->data (list "multipart/alternative;charset=utf-8;"
                          (~a "boundary=\"" boundary "\""))))

(define (make-data-uri str #:content-type [content-type mime-type:plain-text])
  (~a "data:" content-type ";base64,"
      (bytes->string/utf-8 (base64-encode (string->bytes/utf-8 str) #""))))

(define (uuid-v4-string)
  (define bs (crypto-random-bytes 16))
  (define (piece start end)
    (number->string (integer-bytes->integer bs #f (system-big-endian?) start end) 16))
  (string-append (piece 0 4)
                 "-" (piece 4 6)
                 "-" (piece 6 8)
                 "-" (piece 8 10)
                 "-" (piece 10 12) (piece 12 16)))

(define (fresh-boundary-label)
  (string-append "boundary-" (uuid-v4-string)))

(define (split-on-crlf str)
  (string-split str "\r\n" #:trim? #f))

(define (headers->string headers)
  (foldr (λ (header acc) (insert-field (car header) (cdr header) acc)) empty-header headers))

(define (make-message-body content-type content)
  (append (split-on-crlf (headers->string
                          (list (cons "Content-Type" content-type)
                                (cons "Content-Transfer-Encoding" "quoted-printable"))))
          (split-on-crlf (qp-encode (string->bytes/utf-8 content)))))

(struct email (name address) #:transparent
  #:guard (struct-guard/c string? string?))
(define (email-name+address self)
  (~a (email-name self) " <" (email-address self) ">"))

;; ---------------------------------------------------------------------------------------------------
;; pandoc

(define pandoc-path (find-executable-path "pandoc"))

(define meta-json-template (make-data-uri "$meta-json$"))
(define html-email-template
  (make-data-uri
   (xexpr->string
    `(html (head (meta ([http-equiv "Content-Type"] [content ,mime-type:plain-text]))
                 (style ,(~a "span.smallcaps{font-variant:small-caps;}"
                             "span.underline{text-decoration:underline;}"
                             "$if(highlighting-css)$$highlighting-css$$endif$"
                             "p,pre,a.sourceLine{line-height:140%;line-height:1.4;}"
                             ; use specific selector to be sure to override pandoc’s selector
                             "code,pre.sourceCode code{white-space:pre-wrap;}"
                             "pre,div.sourceCode{margin-left:2em;}"
                             "div.sourceCode>pre.sourceCode{margin:0;}")))
           (body "$body$")))))

(define (run-pandoc #:args args
                    #:input [in (open-input-empty)]
                    #:read-output read-out)
  (unless pandoc-path
    (panic! #:name 'pandoc "cannot find executable"))
  (define in-port (if (input-port? in)
                      in
                      (open-input-empty)))
  (define all-args (if (input-port? in)
                       args
                       (append args (list "--" in))))
  (with-resources ([() (make-custodian-resource)])
    (define err (open-output-string))
    (log-send-pandoc-email-debug "~s ~a" pandoc-path (string-join (map ~s all-args) " "))
    (match-define (list out #f _ #f interact)
      (apply process*/ports #f in-port err pandoc-path all-args))
    (begin0
      (read-out out)
      (close-input-port out)
      (interact 'wait)
      (let ()
        (define pandoc-exit-code (interact 'exit-code))
        (define pandoc-err-output (get-output-string err))
        (unless (and (zero? pandoc-exit-code)
                     (zero? (string-length pandoc-err-output)))
          (panic! (string-append "pandoc exited with " (if (not (zero? pandoc-exit-code))
                                                           "non-zero exit code"
                                                           "unclean error output"))
                  #:fields (append (if (zero? pandoc-exit-code) '()
                                       (list (cons "exit code" pandoc-exit-code)))
                                   (if (zero? (string-length pandoc-err-output)) '()
                                       (list (cons "error output" (unquoted-printing-string
                                                                   pandoc-err-output)))))))))))

(define (pandoc #:input in
                #:from [from-format #f]
                #:to to-format
                #:highlight-style [highlight-style "pygments"])
  (run-pandoc #:args (append (if from-format
                                 (list "--from" from-format)
                                 (list))
                             (match to-format
                               ['plain
                                (list "--to" "plain")]
                               ['html
                                (list "--to" "html"
                                      "--template" html-email-template
                                      ; make pandoc not complain if no title is specified for HTML
                                      ; output (since our template doesn’t use it, anyway)
                                      "--variable" "pagetitle:none")]
                               ['meta-json
                                (list "--to" "plain"
                                      "--template" meta-json-template)])
                             (if highlight-style
                                 (list "--highlight-style" highlight-style)
                                 (list "--no-highlight")))
              #:input in
              #:read-output (match to-format
                              [(or 'plain 'html)
                               port->string]
                              ['meta-json
                               read-json])))

;; ---------------------------------------------------------------------------------------------------

(define/contract (json-record/p
                  ps
                  #:absent [absent (json-null)]
                  #:name [name "an object"]
                  #:value-name [value-name (λ (k) (~a "the value for key " (~v (symbol->string k))))])
  (->* [(hash/c symbol? parser?)]
       [#:absent any/c
        #:name (or/c string? #f)
        #:value-name (or/c (-> symbol? string?) #f)]
       (parser/c jsexpr? hash?))
  (describe/p
   #:name name
   (and/last/p
    (satisfy/p hash?)
    (map/p (apply and/list/p
                  (for/list ([(k p) (in-immutable-hash ps)])
                    (map/p #:in (λ (h) (hash-ref h k absent))
                           (describe/p p #:name (and value-name (value-name k)))
                           #:out (λ (v) (cons k v)))))
           #:out make-immutable-hasheq))))

(define (nullable/p p #:default [def-v #f])
  (commit/p (or/p (and/last/p (equal?/p (json-null)) (succeed/p def-v))
                  (and/last/p cut/p p))))

(define any-string/p (describe/p #:name "a string" (satisfy/p string?)))

(define (regexp->string rx)
  (~a "/" (string-replace (object-name rx) "/" "\\/") "/"))
(define (regexp/p rx #:name [name (~a "a string matching " (regexp->string rx))])
  (describe/p #:name name (satisfy/p (λ (v) (and (string? v) (regexp-match? rx v))))))

(define email/p (describe/p
                 #:name "an email"
                 (map/p (json-record/p (hasheq 'name any-string/p
                                               'address any-string/p))
                        #:out (λ (h) (email (hash-ref h 'name) (hash-ref h 'address))))))

(define message-id/p (regexp/p #:name "a message id" #px"^<[^<>]+@[^<>]+>$"))

(define metadata/p
  (map/p (json-record/p
          (hasheq 'email (json-record/p
                          (hasheq 'to email/p
                                  'subject any-string/p
                                  'in-reply-to (nullable/p message-id/p)
                                  'extra-references (nullable/p (listof/p message-id/p)
                                                                #:default '())))))
         #:out (λ (h) (hash-ref h 'email))))

;; ---------------------------------------------------------------------------------------------------

(module+ main
  (require racket/cmdline)

  (define from-format #f)
  (define dry-run? #f)

  (define input-file
    (command-line
     #:once-each
     [("--format") format
      "Specify input format"
      (set! from-format format)]
     [("-n" "--dry-run")
      "Don’t actually send the email, just print what would be sent"
      (set! dry-run? #t)]
     #:args ([input-file #f]) input-file))

  (define input-data (and (not input-file) (port->string (current-input-port))))
  (close-input-port (current-input-port))
  (define (get-input) (or input-file (open-input-string input-data)))

  ;; SMTP + sender configuration
  (define smtp-host "smtp.gmail.com")
  (define smtp-port 587)
  (define smtp-user "lexi.lambda")
  (define smtp-password "REDACTED")

  (define sender (email "Alexis King" "lexi.lambda@gmail.com"))

  ;; Read input metadata
  (define input-metadata (pandoc #:input (get-input) #:from from-format #:to 'meta-json))
  (match-define (hash-table ['to recipient]
                            ['subject subject]
                            ['in-reply-to in-reply-to]
                            ['extra-references extra-references])
    (parse metadata/p input-metadata))

  ;; Read content
  (define plain-content (pandoc #:input (get-input) #:from from-format #:to 'plain))
  (define html-content (pandoc #:input (get-input) #:from from-format #:to 'html))

  ;; Build and send message
  (define alts-label (fresh-boundary-label))

  (define headers-str
    (headers->string
     (append (list (cons "From" (email-name+address sender))
                   (cons "To" (assemble-address-field (list (email-name+address recipient))))
                   (cons "Subject" subject)
                   (cons "MIME-Version" "1.0")
                   (cons "Content-Type" (mime-type:alternative alts-label)))
             (if in-reply-to
                 (list (cons "In-Reply-To" in-reply-to)
                       (cons "References" (data-lines->data (cons in-reply-to extra-references))))
                 '()))))
  (define content-strs (append (list (string-append "--" alts-label))
                               (make-message-body mime-type:plain-text plain-content)
                               (list (string-append "--" alts-label))
                               (make-message-body mime-type:html html-content)
                               (list (string-append "--" alts-label "--"))))

  (cond
    [dry-run?
     (printf "SMTP ~a@~a:~a\n" smtp-user smtp-host smtp-port)
     (printf "FROM ~a\n" (email-address sender))
     (printf "  TO ~a\n\n" (email-address recipient))
     (for-each displayln (split-on-crlf headers-str))
     (for-each displayln content-strs)]
    [else
     (smtp-send-message smtp-host
                        #:port-no smtp-port
                        #:auth-user smtp-user
                        #:auth-passwd smtp-password
                        #:tls-encode ports->ssl-ports
                        (email-address sender)
                        (list (email-address recipient))
                        headers-str
                        content-strs)]))
